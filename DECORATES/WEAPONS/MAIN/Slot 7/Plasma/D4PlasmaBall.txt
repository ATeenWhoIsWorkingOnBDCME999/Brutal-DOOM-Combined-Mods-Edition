ACTOR D4PlasmaBall
{
	Radius 13
	Height 8
	Speed 75
	Damage 4
	Projectile
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1
	DeathSound "plasma/explode/doom4"
	Obituary "%o was melted by %k's plasma rifle."
	Decal Scorch
	DamageType Plasma
	Scale 0.9
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx(PlasmaBallTail,-VelX,-VelY,-VelZ+1.5,VelX,VelY,VelZ,Angle,SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEANGLE|SXF_ABSOLUTEPOSITION)
		PB2A A 1 Bright A_SpawnItemEx(PlasmaBallGlow,0,0,1)
		Loop
	Death:
		TNT1 A 0 A_CustomMissile ("BlueFlare3", 0, 0, random (0, 360), 2, random (0, 360))
		TNT1 A 0 A_PlaySound("plasma/explode/doom4")//Not sure if this will be necessary anymore...it is
		TNT1 A 0 A_SpawnItemEx("PlasmaExplode2",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		PLSY A 0 Bright
		//TNT1 A 0 A_SpawnItem("PlasmaBallImpactFlash")
		TNT1 A 0 A_SpawnItem("PlasmaBallImpactShockWave")
		TNT1 A 0 A_Jump(64,2,3)
		TNT1 AA 0 A_SpawnItem("GenericBolt")
		TNT1 A 0 A_SetScale(FRandom(0.33,0.75))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		PLSY ACE 1 Bright A_Explode(3,50,0)
		PLSY GIKMOP 1 Bright
		Stop
	}
}
Actor PlasmaBallTail : EffectBase
{
	States
	{
	Spawn:
		NULL A 2 Bright
		Stop
	}
}
Actor PlasmaBallGlow : EffectBase
{
	Translation "0:255=%[0,0,0]:[0.66,0.75,1]"
	Scale 0.66
	States
	{
	Spawn:
		TNT1 AA 0 A_SetScale(ScaleX*FRandom(0.33,1.25))
		LENS A 1 Bright
		Stop
	}
}
Actor PlasmaBallImpactShockWave : EffectBase
{
	Scale 0.33
	Translation "0:255=%[0,0,0]:[0,0.5,1.5]"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(ScaleX*FRandom(0.5,1.1))
		X060 H 1 Bright A_FadeOut(0.2)
		TNT1 A 0 A_SetScale(ScaleX*1.5)
		Goto Spawn+1
	}
}
Actor PlasmaBallImpactFlash : EffectBase
{
	Scale 0.75
	States
	{
	Spawn:
		MUZZ GHIJKLQ 0 A_Jump(32,"Anim")
		Loop
	Anim:
		"----" A 1 Bright A_JumpIf(ScaleX<=0,"Null")
		"----" A 0 A_SetScale(ScaleX-0.15)
		Loop
	}
}

Actor GenericBolt : EffectBase
{
	+BRIGHT
	States
	{
	Spawn:
		TNT1 AA 0 A_SetAngle(Random(1,360))
		TNT1 A 0 A_SetPitch(Random(-90,90))
		TNT1 A 0 A_SetScale(ScaleX*FRandom(0.8,1.25))
	Anim:
		NULL AAA 1 A_SetScale(ScaleX/2)
		Stop
	}
}
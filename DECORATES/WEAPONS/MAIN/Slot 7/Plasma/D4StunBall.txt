ACTOR StunBall : ArachnotronPlasma
{
	Damage 10
	SeeSound ""
	Speed 30
	-NOGRAVITY +FORCEXYBILLBOARD// +FORCEPAIN
	Gravity 0.5
	DeathSound "2016Plasma/Stun/Impact"
	Obituary "%k tased the hell outta %o."
	Scale 0.25
	renderstyle add
	Alpha 99
	DamageType Stun
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0.5),Random(1,360))
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,-MomX*0.5,-MomY*0.5,-MomZ*0.5,FRandom(-0.5,0.5),Random(-0.5,0.5),FRandom(-0.5,0.5),0,SXF_ABSOLUTEPOSITION)
		TNT1 AA 0 A_SpawnItemEx(StunBallTrail,Random(0,10),0,Random(-10,10),FRandom(0,0.1),0,FRandom(-0.1,0.1),Random(1,360))
		STBL A 1 A_SpawnItemEx("PlasmaFlare")//AAAABBBBCCCC 1 Bright// A_SpawnItem(StunBallTrail)
		
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,0,0,0,FRandom(0,0.5),0,FRandom(-0.5,0.5),Random(1,360))
		TNT1 A 0 A_SpawnItemEx(StunBallTrail,-MomX*0.5,-MomY*0.5,-MomZ*0.5,FRandom(-0.5,0.5),Random(-0.5,0.5),FRandom(-0.5,0.5),0,SXF_ABSOLUTEPOSITION)
		TNT1 AA 0 A_SpawnItemEx(StunBallTrail,Random(0,10),0,Random(-10,10),FRandom(0,0.1),0,FRandom(-0.1,0.1),Random(1,360))
		STBL A 1 A_SpawnItemEx("PlasmaFlare")//AAAABBBBCCCC 1 Bright// A_SpawnItem(StunBallTrail)
		Loop
	Death:
	XDeath:
		TNT1 A 0
		//TNT1 AAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx(ShockSpark,0,0,0,FRandom(5,10),0,Random(5,10),Random(1,360),SXF_TRANSFERSCALE|1)
		TNT1 A 0 A_SpawnItemEx(GenericBolt,Random(0,20),0,0,0,0,0,Random(1,360),SXF_TRANSFERSCALE|1)
		TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx(StunBallSplash,0,0,0,FRandom(0,4),0,FRandom(4,6),Random(1,360))
		TNT1 AAAA 0 A_SpawnItem(GaussCannonExplosionShockWave)
		TNT1 AAAAAAAA 0 A_SpawnItemEx(GaussExplosionSprite,0,0,0,FRandom(0,8),0,FRandom(-8,8),Random(1,360))
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_CustomMissile(GaussCannonExplosionSpark,5,0,Random(1,360),2,Random(-45,90))
		TNT1 A 0 A_Explode(2,160,0,0,160)
		Stop
	}
}

Actor StunBallTrail : EffectBase
{
	Scale 0.15
	renderstyle add
	ReactionTime 8
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		TNT1 A 0 A_Jump(84, "Anim3")
		TNT1 A 0 A_Jump(193, "Anim2")
	Anim:
		STNB A 1 Bright A_SetScale(ScaleX*0.75)
		TNT1 A 0 A_Countdown
		Loop
	Anim3:
		STNB B 1 Bright A_SetScale(ScaleX*0.75)
		TNT1 A 0 A_Countdown
		Loop
	Anim2:
		STNB C 1 Bright A_SetScale(ScaleX*0.75)
		TNT1 A 0 A_Countdown
		Loop	
	}
}

Actor StunBallSplash : EffectBase
{
	Translation "168:191=[0,255,255]:[0,0,255]", "16:47=[0,255,255]:[0,0,255]"
	Scale 0.75
	-NOINTERACTION
	Gravity 0.33
	Alpha 0.99
	States
	{
	Spawn:
		TNT1 AA 0 A_SetScale(ScaleX*FRandom(0.75,1.2))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(-ScaleX,ScaleY)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SetScale(ScaleX,-ScaleY)
		TNT1 A 0 A_Jump(256,"Anim1","Anim2","Anim3")
	Anim1:
		X057 ABBCDDEFFGHHIJJKKLMMNOOPQQRSS 1 Bright A_FadeOut(0.03)
		Stop
	Anim2:
		X058 ABBCDDEFFGHHIJJKKLMMN 1 Bright A_FadeOut(0.045)
		Stop
	Anim3:
		X059 ABBCDDEFFGHHIJJKKLMM 1 Bright A_FadeOut(0.045)
		Stop
	}
}

Actor StunFXSpawner : CustomInventory
{
	States
	{
	Pickup:
		TNT1 AAA 0 A_SpawnItemEx(StunFXSpark,Random(0,Radius),0,Random(5,Height),0,0,0,Random(1,360))
		TNT1 A 0 A_SpawnItemEx(StunFXVapor,Random(0,Radius/2),0,Random(Height*0.33,Height*0.66),FRandom(0,0.5),0,-0.5,Random(1,360))
		Stop
	}
}

Actor StunFXSpark : EffectBase
{
	Translation "0:255=%[0,0,0]:[1,1,2]"
	Scale 0.0225
	States
	{
	Spawn:
	Decide:
		P001 NOPQ 0 Bright A_Jump(32,"Fade")
		Loop
	Fade:
		"----" A 1 Bright A_SetScale(ScaleX*1.25)
		"----" A 0 A_FadeOut(0.25)
		Loop
	}
}

Actor StunFXVapor : EffectBase
{
	Translation "0:255=%[0,0,0]:[1,1,2]"
	Alpha 0.5
	Scale 0.1
	States
	{
	Spawn:
		P001 K 1 Bright A_FadeOut(0.02)
		TNT1 A 0 A_SetScale(ScaleX+0.01)
		Loop
	}
}
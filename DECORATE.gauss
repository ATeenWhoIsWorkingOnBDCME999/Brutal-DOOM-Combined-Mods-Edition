ACTOR GaussProjectile : FastProjectile
{
  Speed 300
  Radius 10
  Height 7
  Damage 100
  Renderstyle Add
  DamageType Fire
  +RIPPER
  //-CANNOTPUSH
  +EXTREMEDEATH
  +NODAMAGETHRUST
  +DONTBLAST
  MissileType "GaussTrailCalibrator"
  MissileHeight 7
  SeeSound "Weapons/OkudzhavaFly"
  DeathSound "Weapons/OkudzhavaRod"
  states
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 ThrustThingZ(0,10,0,1)
  Looplet:
    TNT1 A 0 ThrustThingZ(0,-2,0,1)
    TNT1 A 1 A_PlaySound("Weapons/OkudzhavaLoop","SoundSlot5",1,1.0,1)
    Loop
  Death:
    TNT1 A 0 A_SpawnItemEx("GaussWallRod",0,0,0,0,0,0,0,128,0)
  XDeath:
    TNT1 A 0 A_StopSound(5)
    TNT1 AAAAAAAAAA 0 A_SpawnItemEx("ExplosionParticleHeavy",random(2,-2),random(2,-2),random(2,-2),random(4,-4),random(4,-4),random(7,-1),random(0,359),128,0)
    TNT1 A 1
    stop
  }
}

ACTOR GaussTrailCalibrator
{
  +NOINTERACTION
  +NOGRAVITY
  States
  {
  Spawn:
    TNT1 A 0
	TNT1 A 0 A_Explode(1,18)
    TNT1 A 0 A_SpawnItemEx("GaussHotRod",0,0,0,frandom(-2,2),frandom(-2,2),frandom(1,3),0,0,0)
    TNT1 A 0 A_SpawnItemEx("ExplosionParticleHeavy",0,0,0,frandom(-2,2),frandom(-2,2),frandom(1,3),0,0,0)
    TNT1 A 0 A_SpawnItemEx("GaussTrail",0,0,0,0,0,0,0,0,0)
    Stop
  }
}

ACTOR GaussHotRod
{
  +NOINTERACTION
  +NOGRAVITY
  Renderstyle Add
  Scale 0.85
  States
  {
  Spawn:
    TNT1 A 0
    GRNP A 1 A_FadeOut(0.2)
    Wait
  }
}

ACTOR GaussWallRod
{
  +NOINTERACTION
  +NOGRAVITY
  +BLOODLESSIMPACT
  DamageType Fire
  ReactionTime 50
  Scale 0.85
  States
  {
  Spawn:
    GRNP A 0
    TNT1 A 0 A_PlaySoundEx("Weapons/OkudzhavaLoop","SoundSlot5",1,-1)
    GRNP A 6 A_Explode(1,18)
    GRNP A 0 A_Jump(128,1)
    GRNP A 0 A_SpawnItemEx("ExplosionParticleHeavy",random(24,-24),3+frandom(1,-1),frandom(1,-1),frandom(-1,1),frandom(-1,1),frandom(2,3),0,0,0)
    GRNP A 0 A_Jump(128,1)
    GRNP A 0 A_SpawnItemEx("ExplosionParticleHeavy",random(24,-24),3+frandom(1,-1),frandom(1,-1),frandom(-1,1),frandom(-1,1),frandom(2,3),0,0,0)
    GRNP A 0 A_Countdown
    Loop
  Death:
    TNT1 A 0
    TNT1 A 0 A_PlaySoundEx("Weapons/OkudzhavaEnd","SoundSlot5",0,-1)
    GRNP A 35
    GRNP A 1 A_FadeOut(0.1)
    Wait
  }
}

ACTOR GaussTrail
{
  +NOINTERACTION
  +NOGRAVITY
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_SpawnItemEx("GaussTrailMain",0.1*random(15,-15),0.1*random(15,-15),0.1*random(15,-15),0,0,0,0,0,0)
    Stop
  }
}

ACTOR GaussTrailMain
{
  RenderStyle Normal
  Scale 0.075
  Alpha 0.35
  +NOINTERACTION
  +NOGRAVITY
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_FadeOut(0.01)
    TNT1 A 0 A_Jump(255,"TrailA","TrailB","TrailC","TrailD")
  TrailA:
    NSMK A 3
  Looper:
    NSMK A 0 A_SetScale(ScaleX +0.005, ScaleY +0.005)
    NSMK A 1 A_FadeOut(0.01)
    Loop
  TrailB:
    NSMK B 3
  Looper:
    NSMK B 0 A_SetScale(ScaleX +0.005, ScaleY +0.005)
    NSMK B 1 A_FadeOut(0.01)
    Loop
  TrailC:
    NSMK C 3
  Looper:
    NSMK C 0 A_SetScale(ScaleX +0.005, ScaleY +0.005)
    NSMK C 1 A_FadeOut(0.01)
    Loop
  TrailD:
    NSMK D 3
  Looper:
    NSMK D 0 A_SetScale(ScaleX +0.005, ScaleY +0.005)
    NSMK D 1 A_FadeOut(0.01)
    Loop
  }
}